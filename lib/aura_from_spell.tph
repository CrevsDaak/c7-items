/* aura_from_spell.tph
 *
 * declares the ADD_ITEM_AURA_EFFECT_FROM_SPELL patch function
 * it basically functions exactly as ADD_ITEM_EQEFFECT except it
 * creates the necessary spells and effect files to apply the effects
 * of the spell over an area while the item is equipped.

 * The 'tobex_id_sign' should be your modding signature's ascii character values in hex.
 *
 * Note that resources are created with destructive naming, eg, you can 100% overwrite old spell or effect files.
 * Cosmetic effects are not copied over.
 *
 */

//TODO
// - allow for multiple spells to be used as source for the auras
//      -> "just reapply the function patch" you end up with multiple op232 per item
// - figure out how the aura AoE works and how I could change their size

DEFINE_PATCH_FUNCTION ADD_ITEM_AURA_EFFECT_FROM_SPELL
  INT_VAR
    tobex_id_sign = 0x3763 //37 63 is '7' 'c'
  STR_VAR
    item = EVAL "%DEST_RES%"
    spell = ""
BEGIN
  PATCH_IF "%spell%" STR_CMP "" && FILE_EXISTS_IN_GAME "%spell%.spl" BEGIN
    INNER_ACTION BEGIN
      COPY_EXISTING - "%spell%.spl" override
        GET_OFFSET_ARRAY ab_array SPL_V10_HEADERS
        PHP_EACH ab_array AS int => ab_off BEGIN
          GET_OFFSET_ARRAY2 fx_array ab_off SPL_V10_HEAD_EFFECTS
          index = 0
          PHP_EACH fx_array AS int => fx_off BEGIN
            READ_SHORT fx_off fx_op
            PATCH_MATCH fx_op WITH
              139 215 174 141 50 9 321 322 336 364 106 23
              BEGIN
                // do nothing, we don't want to copy these effects
              END

              DEFAULT
                SET $fx_to_apply("%index%_opcode")   = SHORT_AT fx_off
                SET $fx_to_apply("%index%_param1")   = LONG_AT (fx_off + 0x4)
                SET $fx_to_apply("%index%_param2")   = LONG_AT (fx_off + 0x8)
                SET $fx_to_apply("%index%_dicenum")  = LONG_AT (fx_off + 0x1c)
                SET $fx_to_apply("%index%_dicesize") = LONG_AT (fx_off + 0x20)
                PATCH_IF ENGINE_IS tob BEGIN
                  SET $fx_to_apply("%index%_svthrow")  = LONG_AT (fx_off + 0x24) BOR BIT25
                  SET $fx_to_apply("%index%_tobexid")  = tobex_id_sign << 2 + index
                END ELSE BEGIN
                  SET $fx_to_apply("%index%_svthrow")  = LONG_AT (fx_off + 0x24)
                  SET $fx_to_apply("%index%_tobexid")  = LONG_AT (fx_off + 0x2c)
                END
                SET $fx_to_apply("%index%_svbonus")  = LONG_AT (fx_off + 0x28)

                READ_ASCII (fx_off + 0x14) fx_res
                TEXT_SPRINT $fx_to_apply("%index%_res") EVAL "%fx_res%"

                ++index
            END
          END
          SET $fx_to_apply("idx") = index
        END
      BUT_ONLY

      // should get changed to pick new, unused names via unique name function
//      TEXT_SPRINT c7__aura_effect_name   "%item%"
      OUTER_SPRINT c7__aura_spell_name    "%item%"
/*
      // create an effect that applies a spell. very simple stuff
      CREATE EFF "%c7__aura_effect_name%"
        WRITE_LONG 0x10 146 //opcode
        WRITE_LONG 0x14 1 //target
        WRITE_LONG 0x20 1 //mode
        WRITE_SHORT 0x2c 100 //probability1
        WRITE_ASCIIE 0x30 "%c7__aura_spell_name%"
*/
      // creates a blank spell w/blank ability, then adds in the effects via function calls
      CREATE SPL "%c7__aura_spell_name%"
        WRITE_LONG 0x8 "-1" //blank out name
        WRITE_LONG 0xC "-1"
        WRITE_SHORT 0x1c 4 //innate
        WRITE_SHORT 0x68 1 //ab count
        INSERT_BYTES 0x72 0x28 //add spl ab header
        WRITE_BYTE 0x72 1 //head type
        WRITE_SHORT 0x74 2 //ab type
        WRITE_BYTE 0x7e 1 //target
        WRITE_SHORT (0x72 + 0x26) 158 //projectile, INAREANP.pro
        fx_count = fx_to_apply_idx
        FOR (i=0; i < fx_count; ++i) BEGIN
          LPF ADD_SPELL_EFFECT
            INT_VAR
              opcode        = $fx_to_apply("%i%_opcode")
              target        = 3
              duration      = 7
              parameter1    = $fx_to_apply("%i%_param1")
              parameter2    = $fx_to_apply("%i%_param2")
              dicenumber    = $fx_to_apply("%i%_dicenum")
              dicesize      = $fx_to_apply("%i%_dicesize")
              savingthrow   = $fx_to_apply("%i%_svthrow")
              savebonus     = $fx_to_apply("%i%_svbonus")
              special       = $fx_to_apply("%i%_tobexid")
            STR_VAR
              resource      = EVAL $fx_to_apply("%i%_res")
          END
        END
        PATCH_IF GAME_IS ~eet bgee bg2ee~ BEGIN
          LPF ADD_SPELL_EFFECT
            INT_VAR
              opcode        = 321
              target        = 3
              param2        = 2
            STR_VAR
              resource      = EVAL "%c7__aura_spell_name%"
          END
        END
    END
    
    LPF ADD_ITEM_EQEFFECT
      INT_VAR
        opcode          = 232
        target          = 1
        parameter2      = 9
        timing          = 2
      STR_VAR
        resource = EVAL "%c7__aura_spell_name%"
    END
  END ELSE BEGIN
    PATCH_WARN ~WARNING: Failed to apply ADD_ITEM_AURA_EFFECT_FROM_SPELL to %item%.itm due to missing %spell%.spl!~
  END
END
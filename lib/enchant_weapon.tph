
// enchant_weapon.tph
// Time-stamp: </Users/nico/BG_modding/c7-items/lib/enchant_weapon.tph, 2024-03-09 Saturday 18:55:45 nico>

DEFINE_PATCH_FUNCTION c7_enchant_update_name
  INT_VAR
    enchantment = 1
  STR_VAR
    item_name = ""
BEGIN
  PATCH_IF "%item_name%" STR_EQ "" BEGIN
    READ_STRREF 0xC item_name
  END
  INNER_PATCH_SAVE updated_item_name "%item_name%" BEGIN
    PATCH_IF "%item_name%" STRING_CONTAINS_REGEXP "\+[1-9]$" BEGIN
      REPLACE_TEXTUALLY ~^\(.+\)$~ ~\1 +%enchantment%~
    END ELSE BEGIN
      REPLACE_TEXTUALLY ~\+[1-9]$~ ~+%enchantment%~
    END
  END
  SAY 0xC "%updated_item_name%"
END

DEFINE_PATCH_FUNCTION c7_enchant_weapon
  INT_VAR
    enchantment = 1
    thac0_bonus = enchantment
    damage_bonus = enchantment
    hit_level = enchantment
    update_name = 0
  STR_VAR
    item_name = ""
BEGIN
  PATCH_IF update_name BEGIN
    LPF c7_enchant_update_name INT_VAR enchantment STR_VAR item_name END
  END

  READ_LONG 0x18 item_flags
  WRITE_LONG 0x18 (item_flags BOR BIT6)

  READ_LONG 0x60 old_enchant_lvl
  WRITE_LONG 0x60 hit_level
  GET_OFFSET_ARRAY ab_array ITM_V10_HEADERS
  PHP_EACH ab_array AS int => ab_off BEGIN
    PATCH_IF BYTE_AT ab_off != 3 BEGIN
      READ_BYTE (ab_off + 0x12) speed_factor //avoid writing negatives
      WRITE_BYTE (ab_off + 0x12) (speed_factor + old_enchant_lvl - enchantment) >= 0 ? (speed_factor + old_enchant_lvl - enchantment) : 0

      READ_BYTE (ab_off + 0x26) flags
      PATCH_IF flags BAND BIT1 == BIT1 BEGIN
        WRITE_BYTE (ab_off + 0x26) (flags BXOR BIT1)
      END
    END
  END
  LPF ALTER_EFFECT
    INT_VAR
      check_headers    = 0
      match_opcode     = 233
      match_parameter2 = 109
      match_timing     = 2
      parameter1 = enchantment
  END
  // while i could also drop in match_probability1 = 1, I think there's no point
  PATCH_IF !ENGINE_IS tob BEGIN
    LPF DELETE_EFFECT
      INT_VAR
        check_globals = 0
        match_opcode  = 146
        header_type   = 1
      STR_VAR
        match_resource = EVAL "%SOURCE_RES%"
    END
  END ELSE PATCH_IF GAME_IS bgt BEGIN
    LPF DELETE_EFFECT
      INT_VAR
        check_globals = 0
        header_type   = 1
        match_opcode  = 309
      STR_VAR
        match_resource = EVAL "%SOURCE_RES%"
    END
  END
  // could be more efficient and not call this for every possible header type, but meehh
  PATCH_FOR_EACH header_type IN 1 2 4 BEGIN
    LPF ALTER_ITEM_HEADER INT_VAR header_type damage_bonus thac0_bonus END
  END
END

DEFINE_PATCH_FUNCTION c7_enchant_armour
  INT_VAR
    enchantment = 1
    ac_bonus    = enchantment
    itemexcl_a  = 1
    update_name = 0
  STR_VAR
    item_name = ""
BEGIN
  PATCH_IF enchantment && FILE_EXISTS_IN_GAME "itemexcl.2da" && itemexcl_a && SHORT_AT 0x1c == 2 BEGIN
    INNER_ACTION BEGIN
      APPEND ~%DEST_RES%  1~ UNLESS ~%DEST_RES%~
    END
  END

  PATCH_IF update_name BEGIN
    LPF c7_enchant_update_name INT_VAR enchantment STR_VAR item_name END
  END

  READ_LONG 0x18 item_flags
  WRITE_LONG 0x18 (item_flags BOR BIT6)

  READ_LONG 0x60 old_enchant_lvl
  WRITE_LONG 0x60 enchantment

  GET_OFFSET_ARRAY ITM_V10_GEN_EFFECTS
  PHP_EACH fx_array AS int => fx_off BEGIN
    PATCH_IF (SHORT_AT fx_off) == 0 && LONG_AT (fx_off + 0x8) == BIT4 && BYTE_AT (fx_off + 0xC) BEGIN
      READ_LONG  (fx_off + 0x4) ac
      WRITE_LONG (fx_off + 0x4) (ac - enchantment + old_enchant_lvl)
    END
  END
END

  SET books = 0x00
  SET amulets = 0x01
  SET armor = 0x02
  SET belts = 0x03
  SET boots = 0x04
  SET arrows = 0x05
  SET gauntlets = 0x06
  SET headgear = 0x07
  SET keys = 0x08
  SET potions = 0x09
  SET rings = 0x0a
  SET scrolls = 0x0b
  SET shields_not_iwd = 0x0c
  SET food = 0x0d
  SET bullets = 0x0e
  SET bows = 0x0f
  SET daggers = 0x10
  SET maces_clubs = 0x11
  SET slings = 0x12
  SET small_swords = 0x13
  SET large_swords = 0x14
  SET hammers = 0x15
  SET morning_stars = 0x16
  SET flails = 0x17
  SET darts = 0x18
  SET axes = 0x19
  SET quarterstaff = 0x1a
  SET crossbow = 0x1b
  SET hand_to_hand_weapons = 0x1c
  SET spears = 0x1d
  SET halberds = 0x1e
  SET crossbow_bolts = 0x1f
  SET cloaks_and_robes = 0x20
  SET gold_pieces = 0x21
  SET gems = 0x22
  SET wands = 0x23
  SET containers_eye_broken_armor = 0x24
  SET books_broken_shields_bracelets = 0x25
  SET familiars_broken_swords_earrings = 0x26
  SET tattoos_pst = 0x27
  SET lenses_pst = 0x28
  SET bucklers = 0x29
  SET candles = 0x2a
  SET clubs_iwd = 0x2c
  SET large_shields_iwd = 0x2f
  SET medium_shields_iwd = 0x31
  SET notes = 0x32
  SET small_shields_iwd = 0x35
  SET telescopes_iwd = 0x37
  SET drinks_iwd = 0x38
  SET great_swords_iwd = 0x39
  SET container = 0x3a
  SET fur_pelt = 0x3b
  SET leather_armor = 0x3c
  SET studded_leather_armor = 0x3d
  SET chain_mail = 0x3e
  SET splint_mail = 0x3f
  SET half_plate = 0x40
  SET full_plate = 0x41
  SET hide_armor = 0x42
  SET robe = 0x43
  SET bastard_sword = 0x45
  SET scarf = 0x46
  SET food_iwd2 = 0x47
  SET hat = 0x48
  SET gauntlet = 0x49